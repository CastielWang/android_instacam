/*
   Copyright 2012 Harri Smatt

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
 */

precision mediump float;

uniform sampler2D sTexture;

uniform float uBrightness;
uniform float uContrast;
uniform float uSaturation;
uniform float uCornerRadius;

varying vec2 vTextureCoord;

void main() {
	vec3 color = texture2D(sTexture, vTextureCoord).rgb;
	
	color += uBrightness;
	if (uContrast > 0.0) {
		color = (color - 0.5) / (1.0 - uContrast) + 0.5;
	} else {
		color = (color - 0.5) * (1.0 + uContrast) + 0.5;
	}
	float average = dot(color, vec3(1.0)) / 3.0;
	if (uSaturation > 0.0) {
		color += (average - color) * (1.0 - 1.0 / (1.0 - uSaturation));
	} else {
		color += (average - color) * (-uSaturation);
	}
	
	const float sqrt2 = 1.414213562373;
	float len = length(vTextureCoord - 0.5) * sqrt2;
	len = smoothstep(1.0 - uCornerRadius, 1.0, len);
	color *= mix(0.15, 1.0, 1.0 - len);
	
	gl_FragColor = vec4(color, 1.0);
}
