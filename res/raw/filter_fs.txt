/*
   Copyright 2012 Harri Smatt

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
 */

precision mediump float;

uniform sampler2D sTexture;

uniform int uFilter;
uniform float uBrightness;
uniform float uContrast;
uniform float uSaturation;
uniform float uCornerRadius;
uniform vec2 uPixelSize;

varying vec2 vTextureCoord;

vec3 brightness(vec3 color, float brightness) {
	float scaled = brightness / 2.0;
	if (scaled < 0.0) {
		return color * (1.0 + scaled);
	} else {
		return color + ((1.0 - color) * scaled);
	}
}

vec3 contrast(vec3 color, float contrast) {
	const float PI = 3.14159265;
	return min(vec3(1.0), ((color - 0.5) * (tan((contrast + 1.0) * PI / 4.0) ) + 0.5));
}

vec3 overlay(vec3 overlayComponent, vec3 underlayComponent, float alpha) {
	vec3 underlay = underlayComponent * alpha;
	return underlay * (underlay + (2.0 * overlayComponent * (1.0 - underlay)));
}

vec3 multiplyWithAlpha(vec3 overlayComponent, float alpha, vec3 underlayComponent) {
	return underlayComponent * overlayComponent * alpha;
}

vec3 screenPixelComponent(vec3 maskPixelComponent, float alpha, vec3 imagePixelComponent) {
	return 1.0 - (1.0 - (maskPixelComponent * alpha)) * (1.0 - imagePixelComponent);
}

vec3 rgbToHsv(vec3 color) {
	vec3 hsv;
	
	float mmin = min(color.r, min(color.g, color.b));
	float mmax = max(color.r, max(color.g, color.b));
	float delta = mmax - mmin;
	
	hsv.z = mmax;
	hsv.y = delta / mmax;

	if (color.r == mmax) {
		hsv.x = (color.g - color.b) / delta;
	} else if (color.g == mmax) {
		hsv.x = 2.0 + (color.b - color.r) / delta;
	} else {
		hsv.x = 4.0 + (color.r - color.g) / delta;
	}
	
	hsv.x *= 0.166667;
	if (hsv.x < 0.0) {
		hsv.x += 1.0;
	}
	
	return hsv;
}

vec3 hsvToRgb(vec3 hsv) {
	if (hsv.y == 0.0) {
		return vec3(hsv.z);
	} else {
		float i;
		float aa, bb, cc, f;

		float h = hsv.x;
		float s = hsv.y;
		float b = hsv.z;

		if (h == 1.0) {
			h = 0.0;
		}

		h *= 6.0;
		i = floor(h);
		f = h - i;
		aa = b * (1.0 - s);
		bb = b * (1.0 - (s * f));
		cc = b * (1.0 - (s * (1.0 - f)));
		
		if (i == 0.0) return vec3(b, cc, aa);
		if (i == 1.0) return vec3(bb, b, aa);
		if (i == 2.0) return vec3(aa, b, cc);
		if (i == 3.0) return vec3(aa, bb, b);
		if (i == 4.0) return vec3(cc, aa, b);
		if (i == 5.0) return vec3(b, aa, bb);
	}
}

vec3 saturation(vec3 color, float sat) {
	const float lumaR = 0.212671;
	const float lumaG = 0.715160;
	const float lumaB = 0.072169;
	
	float v = sat + 1.0;
	float i = 1.0 - v;
	float r = i * lumaR;
	float g = i * lumaG;
	float b = i * lumaB;
	
	mat3 mat = mat3(r + v, r, r, g, g + v, g, b, b, b + v);
	
	return mat * color;
}

void main() {
	vec3 color = texture2D(sTexture, vTextureCoord).rgb;
	
	// Black and White filter.
	if (uFilter == 1) {
		float gray = dot(color, vec3(0.299, 0.587, 0.114));
		color = vec3(gray);
	}
	// Ansel filter.
	if (uFilter == 2) {
		float gray = dot(color, vec3(0.299, 0.587, 0.114));
		if (gray > 0.5) {
			color = vec3(1.0 - (1.0 - 2.0 * (gray - 0.5)) * (1.0 - gray));
		} else {
			color = vec3(2.0 * gray * gray);
		}
	}
	// Sepia filter.
	if (uFilter == 3) {
		float luminosity = dot(color, vec3(0.21, 0.72, 0.07));
		float brightGray = brightness(vec3(luminosity), 0.234375).r;
		
		vec3 tinted = overlay(vec3(0.419, 0.259, 0.047), vec3(brightGray), 1.0);
		
		float invertMask = 1.0 - luminosity;
		float luminosity3 = pow(luminosity, 3.0);
		
		color = vec3(luminosity3) + (tinted * invertMask * (luminosity + 1.0));
	}
	// Retro filter.
	if (uFilter == 4) {
		float gray = dot(color, vec3(0.299, 0.587, 0.114));		
		color = overlay(vec3(gray), color, 1.0);
		color = multiplyWithAlpha(vec3(0.984, 0.949, 0.639), 0.588235, color);
		color = screenPixelComponent(vec3(0.909, 0.396, 0.702), 0.2, color);
		color = screenPixelComponent(vec3(0.035, 0.286, 0.914), 0.168627, color);
	}
	// Georgia filter.
	if (uFilter == 5) {		
		color = brightness(color, 0.4724);
		color = contrast(color, 0.3149);
		
		color.g = color.g * 0.87 + 0.13;
		color.b = color.b * 0.439 + 0.561;
		
		color *= vec3(0.981, 0.862, 0.686);
	}
	// Sahara filter.
	if (uFilter == 6) {
		color.r = color.r * 0.843 + 0.157;
		color.b = color.b * 0.882 + 0.118;
		
		vec3 hsv = rgbToHsv(color);
		hsv.y = hsv.y * 0.55;
		color = hsvToRgb(hsv);
		
		color = saturation(color, 0.65);
		color *= vec3(1.0, 0.891, 0.733);
	}
	// Polaroid filter.
	if (uFilter == 7) {
		const mat4 mat = mat4(1.438, -0.062, -0.062, 0.0,
		                      -0.122, 1.378, -0.122, 0.0,
		                      -0.016, -0.016, 1.483, 0.0,
		                      -0.03, 0.05, -0.02, 0.0);
		color = (mat * vec4(color, 1.0)).rgb;
	}
	// Cartoon filter.
	if (uFilter == 8) {
		vec3 sample[9];
		vec3 border;		
		vec2 texCoord;
		
		int i = 0;
		color = vec3(0.0, 0.0, 0.0);
		texCoord.x = vTextureCoord.x - uPixelSize.x * 2.0;
		for (int x = 0; x < 3; ++x) {
			texCoord.y = vTextureCoord.y + uPixelSize.y * 2.0;
			for (int y = 0; y < 3; ++y) {
				sample[i] = texture2D(sTexture, texCoord).rgb;
				color += sample[i++];
				texCoord.y -= uPixelSize.y * 2.0;
			}
			texCoord.x += uPixelSize.x * 2.0;
		}
		color /= 9.0;
		
		vec3 horizEdge = sample[2] + sample[5] + sample[8] -
					(sample[0] + sample[3] + sample[6]);

		vec3 vertEdge = sample[0] + sample[1] + sample[2] -
						(sample[6] + sample[7] + sample[8]);

		border = sqrt((horizEdge * horizEdge) + 
						(vertEdge * vertEdge));

		if (border.r > 0.4 || border.g > 0.4 || border.b > 0.4){
			color *= 1.0 - dot(border, border);
		}
		
		const vec3 colorRed = vec3(1.0, 0.3, 0.3);
		const vec3 colorGreen = vec3(0.3, 1.0, 0.3);
		const vec3 colorBlue = vec3(0.3, 0.3, 1.0);
		
		color = floor(color * 4.0) * 0.25;
		color = colorRed * color.r + colorBlue * color.b + colorGreen * color.g;
	}
	
	// Calculate brightness, contrast and saturation.
	color = brightness(color, uBrightness);
	color = contrast(color, uContrast);
	color = saturation(color, uSaturation);
	
	// Calculate darkened corners.
	const float sqrt2 = 1.414213562373;
	float len = distance(vTextureCoord, vec2(0.5)) * sqrt2;
	len = smoothstep(1.0 - uCornerRadius, 1.0, len);
	color *= mix(0.5, 1.0, 1.0 - len);
	
	gl_FragColor = vec4(color, 1.0);
}
